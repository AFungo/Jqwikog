repositories {
	mavenCentral()
	//maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

ext.junitPlatformVersion = '1.3.1'
ext.junitJupiterVersion = '5.3.1'
ext.moduleName = 'net.jqwik'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'net.jqwik'
version = '0.9.3-SNAPSHOT'

jar {
	baseName = 'jqwik'
	version = '0.9.3-SNAPSHOT'
	manifest {
		attributes('Automatic-Module-Name': moduleName)
	}
}

task javadocApi(type: Javadoc) {
	source = file('./src/main/java')
	failOnError = false
	destinationDir = file('./docs/javadoc')
	include '**/api/**'
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar
}

signing {
	sign configurations.archives
}

def isSnapshot = project.version.contains('SNAPSHOT')
def signArtifacts = !isSnapshot

uploadArchives {

	dependsOn check

	repositories {
		mavenDeployer {

			if (signArtifacts) {
				beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
			}

			def ossrhUsername = project.hasProperty('ossrhUsername') ? project.ossrhUsername : ''
			def ossrhPassword = project.hasProperty('ossrhPassword') ? project.ossrhPassword : ''

			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			pom.project {
				name 'jqwik'
				packaging 'jar'
				description 'jqwik - Property-based testing for the JVM'
				url 'http://jqwik.net'

				scm {
					connection 'scm:git:git://github.com/jlink/jqwik.git'
					developerConnection 'scm:git:git://github.com/jlink/jqwik.git'
					url 'https://github.com/jlink/jqwik'
				}

				licenses {
					license {
						name 'Eclipse Public License - v 2.0'
						url 'http://www.eclipse.org/legal/epl-v20.html'
					}
				}

				developers {
					developer {
						id 'jlink'
						name 'Johannes Link'
						email 'business@johanneslink.net'
					}
				}
			}
		}
	}
}

compileTestJava {
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
	options.compilerArgs += '-parameters'
	options.encoding = 'UTF-8'
}

test {
	useJUnitPlatform {
		includeEngines "jqwik"
		includeEngines "junit-archunit"
	}


	include 'net/**/*Properties.class'
	include 'net/**/*Example.class'
	include 'net/**/*Examples.class'
	include 'net/**/*Test.class'
	include 'net/**/*Tests.class'

	// testLogging.showStandardStreams = true
}

dependencies {
	compile("org.junit.platform:junit-platform-engine:${junitPlatformVersion}")
	compile("org.junit.platform:junit-platform-commons:${junitPlatformVersion}")
	compile("org.apiguardian:apiguardian-api:1.0.0")

	testCompile("org.mockito:mockito-core:2.23.0")
	testCompile("org.assertj:assertj-core:3.11.1")
	testCompile("org.junit.platform:junit-platform-launcher:${junitPlatformVersion}")

	// Only needed to check interferences between Jupiter and jqwik
	testCompile("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}")

	testCompile "com.tngtech.archunit:archunit-junit5-api:0.9.1"
	testRuntime "com.tngtech.archunit:archunit-junit5-engine:0.9.1"

}

wrapper {
	description = 'Generates gradlew[.bat] scripts'
	gradleVersion = '4.10.2'
}
